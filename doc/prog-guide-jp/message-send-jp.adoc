== メッセージと送信式

=== メッセージ

メッセージは以下のように分類されます。

* インスタンスメッセージ
** 単純インスタンスメッセージ
*** 単項インスタンスメッセージ
**** [例] Int#to-s : String
**** [例] Bool#not : Bool
**** [例] List#join : String
*** 二項インスタンスメッセージ
**** [例] Int#+ : Int -> Int
**** [例] Int#to : Int -> Interval
**** [例] String#^ : String -> String
**** [例] List#cons : Top -> List
**** [例] List#join-by : String -> String
**** [例] Fun#apply : Top -> Top
*** 多項インスタンスメッセージ
**** [例] Int#to-by : Int -> Int -> Interval
**** [例] List#foldr : Top -> Fun -> Top
**** [例] Fun#apply-binary : Top -> Top -> Top
**** [例] Fun#apply-nary : Top -> Top -> Morph -> Top
** キーワードインスタンスメッセージ
*** [例] Int#(to:Int) -> Interval
*** [例] Int#(to:Int by:Int) -> Interval
*** [例] List#(join:String) -> String
* クラスメッセージ
** 単純クラスメッセージ
*** [例] &Bool.true : Bool
*** [例] &Float.nan : Float
*** [例] &Some.make : Top -> Some
*** [例] &Datum.make : Symbol -> Top -> Datum
*** [例] &List.cons : Top -> Morph -> Morph
*** [例] &Interval.make : Int -> Int -> Interval
*** [例] &Interval.make-by : Int -> Int -> Int -> Interval
** キーワードクラスメッセージ
*** [例] &Datum.(tag:Symbol contents:Top) -> Datum
*** [例] &List.(head:Top tail:Morph) -> Morph
*** [例] &Interval.(from:Int to:Int) -> Interval
*** [例] &Interval.(from:Int to:Int by:Int) -> Interval


=== インスタンスメッセージとクラスメッセージ

インスタンスメッセージは普通のメッセージです。

クラスメッセージはクラス式 `&Foo` で生成されるクラスオブジェクトへのメッセージです。
これは多くの場合、以下の目的で用いられます。

- あるクラスからインスタンスを生成するオブジェクト構成子(object constructor)を提供
- 非オブジェクト指向な手続き型言語/関数型言語におけるライブラリ関数やシステムコールを提供

> [!NOTE]
> 現在、JavaやRubyで見かけるインスタンス生成の予約語 `new` は、定義されていますが使われていません。
> 将来、クラスのユーザー定義機能が提供される時、`new Foo x` という文法で使われるようになる予定です。


=== 単純メッセージとキーワードメッセージ

単純メッセージはカリー化されるメッセージであり、オブジェクト指向と関数型を混在したプログラミングスタイルに適しています。

キーワードメッセージはカリー化されないメッセージであり、複雑な引数を伴うメッセージでは単純メッセージよりも可読性が優れています。



=== 関数適用とメッセージ送信

```
umu:1> 3 + 4             # 中置演算子式
val it : Int = 7
umu:2> (+) 3 4           # 関数オブジェクトとしての中置演算子
val it : Int = 7
umu:3> ((+) 3) 4         # 第一引数の部分適用
val it : Int = 7
umu:4> (+ 4) 3           # 第二引数の部分適用
val it : Int = 7
```

```
umu:5> 3.(+ 4)           # 二項メッセージ '+4' を整数オブジェクト 3 へ送信
val it : Int = 7
umu:6> 3.+ 4             # カッコは省略できます
val it : Int = 7
umu:7> (3.+) 4           # 二項メッセージのキーワード '+' だけを送信
val it : Int = 7
umu:8> &(+) 3 4          # 関数オブジェクトとしての二項メッセージ
val it : Int = 7
umu:9> (&(+) 3) 4        # 第一引数の部分適用
val it : Int = 7
```

```
umu:10> (+).apply-binary 3 4    # 関数オブジェクトに適用メッセージを送信
val it : Int = 7
umu:11> (+).[3, 4]       # もう一つの適用メッセージ記法(構文糖)
val it : Int = 7
```



=== メッセージチェイン、パイプライン適用そして関数合成

ここで解説するプログラム表現の考察は、edvakfさんのブログ記事:
link:https://edvakf.hatenadiary.org/entry/20090405/1238885788[PythonでもRubyみたいに配列をメソッドチェーンでつなげたい]
を出発点としています。


==== メッセージチェイン

オブジェクト指向プログラミングの標準的なスタイルであり、
複数のメッセージを送信式で繋ぐ(chain)ことによって、
左から右へと流れるようなコードが書けます。

```
umu:1> [1, 4, 3, 2]
val it : Cons = [1, 4, 3, 2]
umu:2> [1, 4, 3, 2].sort
val it : Cons = [1, 2, 3, 4]
umu:3> [1, 4, 3, 2].sort.reverse
val it : Cons = [4, 3, 2, 1]
umu:4> [1, 4, 3, 2].sort.reverse.map to-s
val it : Cons = ["4", "3", "2", "1"]
umu:5> [1, 4, 3, 2].sort.reverse.map to-s.join-by "-"
val it : String = "4-3-2-1"
umu:6>
```

==== パイプライン適用

F#, Ocaml, Scala, Elixir のように ...

関数型プログラミングのスタイルの一つであり、
一つの式と複数の関数をパイプライン演算子 `|>` で連結(concatenate)することによって、
左から右へと流れるようなコードが書けます。

```
umu:1> [1, 4, 3, 2] |> sort |> reverse |> map to-s |> join-by "-"
val it : String = "4-3-2-1"
umu:2>
```

==== もう一つのパイプライン適用

(2) と似ていますが、こちらは値がパイプラインを右から左へと流れます。

このスタイルは、あまりに過剰なカッコに疲れ果てている全世界のプログラマーにとって、救いの手となるでしょう。

特に `Haskell` だとパイプライン演算子は標準演算子 `$` として定義され、好んで広く使われています。

```
umu:1> join-by "-" <| map to-s <| reverse <| sort [1, 4, 3, 2]
val it : String = "4-3-2-1"
umu:2>
```

==== 関数合成

ここまで述べたメッセージチェインやパイプライン適用といったプログラミング技法よりも更に視点を上に向け、
設計技法として「コードの部品化と再利用」を推進するのが関数合成です。

以下の例では、４個の部品 `sort`、 `reverse`、 `map to-s` そして`join-by "-"` について、
関数合成演算子 `>>` で左から右へと合成し、完成した関数オブジェクトを `f` として定義しています。

```
umu:1> val f = sort >> reverse >> map to-s >> join-by "-"
fun f = #<{ %x -> (%x |> sort |> reverse |> (map to-s) |> (join-by "-")) }>
umu:2> f [1, 4, 3, 2]
val it : String = "4-3-2-1"
umu:3
```

==== もう一つの関数合成

(3) と似ていますが、こちらは関数合成演算子 `<<` で部品を右から左へ合成します。

`Haskell` だと関数合成演算子は標準演算子 `.` として定義され、
このスタイルがポイントフリースタイル(point free style)と命名されるほど広く知られ、
好んで使われています。

```
umu:1> val f = join-by "-" << map to-s << reverse << sort
fun f = #<{ %x -> (%x |> sort |> reverse |> (map to-s) |> (join-by "-")) }>
umu:2> f [1, 4, 3, 2]
val it : String = "4-3-2-1"
umu:3>
```

==== 伝統的な入れ子になった関数適用

Lisp, Python, Pascal, Fortran のように ...

科学技術計算のような数値関数ライブラリであれば、あえて伝統的なスタイルを採用することも検討すべきでしょう。

```
umu:1> join-by "-" (map to-s (reverse (sort [1, 4, 3, 2])))
val it : String = "4-3-2-1"
umu:2>
```

