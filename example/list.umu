structure List = struct {
    # head : ['a] -> 'a
    fun head = [x|_] -> x


    # tail : ['a] -> ['a]
    fun tail = [_|xs] -> xs


    # singleton : 'a -> ['a]
    fun singleton = x -> [x]


    # singleton? : ['a] -> BOOL
    fun singleton? = xs -> case xs of {
    [_|xs] -> empty? xs
    else   -> FALSE
    }


    # equal? : ('a -> 'b -> BOOL) -> 'A -> 'B -> BOOL
    (#
        where, if
        * 'A is a List and 'a is element of 'A
        * 'B is a List and 'b is element of 'B
        * equal 'a to 'b
        then TRUE, else FALSE
    #)
    fun rec equal? = eq? xs ys ->
        if xs kind-of? List && ys kind-of? List then
            case xs of {
            | []      -> empty? ys
            | [x|xs'] ->
                case ys of {
                | []      -> FALSE
                | [y|ys'] -> eq? x y && equal? eq? xs' ys'
                }
            }
        else
            FALSE


    # fold : 'b -> ('a -> 'b -> 'b) -> ['a] -> 'b
    fun rec fold = a f xs -> case xs of {
    | []      -> a
    | [x|xs'] -> fold (f x a) f xs'
    }


    # foldr : 'b -> ('a -> 'b -> 'b) -> ['a] -> 'b
    fun rec foldr = a f xs -> case xs of {
    | []      -> a
    | [x|xs'] -> f x (foldr a f xs')
    }


    # foldl : 'b -> ('b -> 'a -> 'b) -> ['a] -> 'b
    fun rec foldl = a f xs -> case xs of {
    | []      -> a
    | [x|xs'] -> foldl (f a x) f xs'
    }


    # length : ['a] -> INT
    fun rec length = xs -> case xs of {
    | []      -> 0
    | [x|xs'] -> length xs' + 1
    }

    val length' = fold 0 { _ len -> len + 1 }


    # sum : [INT] -> INT
    fun rec sum = xs -> case xs of {
    | []      -> 0
    | [x|xs'] -> x + sum xs'
    }

    val sum' = fold 0 (+)


    # map : ('a -> 'b) -> ['a] -> ['b]
    fun rec map = f xs -> case xs of {
    | []      -> []
    | [x|xs'] -> [f x | map f xs']
    }

    fun map' = f -> foldr [] { x xs -> [f x | xs] }


    # select : ('a -> BOOL) -> ['a] -> ['a]
    fun rec select = f xs -> case xs of {
    | []      -> []
    | [x|xs'] -> if f x then xs' else xs'
    }

    fun select' = f -> foldr [] { x xs -> if f x then xs' else xs }


    # append : ['a] -> ['a] -> ['a]
    fun rec append = xs ys -> case xs of {
    | []   -> ys
    | [x|xs'] -> case ys of {
          | [] -> xs
          else -> [x | append xs' ys]
        }
    }

    fun append' = xs ys -> foldr ys cons xs
    val append'' = (++)


    # concat : [['a]] -> ['a]
    fun rec concat = xss -> case xss of {
    | []        -> []
    | [xs|xss'] -> xs ++ concat xss'
    }

    val concat'  = fold  [] { xs xss -> xss ++ xs }
    val concat'' = foldr [] (++)


    # join-string : String -> [String] -> String
    fun join-string = j xs -> case xs of {
    | []      -> ""
    | [x|xs'] -> case xs' of {
          | [] -> x
          else -> x ^ fold "" { x' s -> s ^ j ^ x' } xs'
        }
    }


    # reverse : ['a] -> ['a]
    fun rec reverse = xs -> case xs of {
    | []      -> []
    | [x|xs'] -> reverse xs' ++ [x]
    }

    val reverse' = rev []
    where {
        fun rec rev = a xs -> case xs of {
        | []      -> a
        | [x|xs'] -> rev [x|a] xs'
        }
    }

    val reverse'' = fold [] cons


    # zip : ['a] -> ['b] -> [('a, 'b)]
    fun rec zip = xs ys -> case xs of {
    | []      -> ys
    | [x|xs'] -> case ys of {
        | []      -> xs
        | [y|ys'] -> [(x, y) | zip xs' ys']
        }
    }

    val zip' = foldr (const []) f
    where {
        fun f = x g ys -> case ys of {
        | []      -> []
        | [y|ys'] -> [(x, y) | g ys']
        }
    }


    # unzip : [('a, 'b)] -> (['a], ['b])
    fun rec unzip = xs -> case xs of {
    | []         -> ([], [])
    | [pair|xs'] -> let {
                        val (y, z)     = pair
                        val (ys', zs') = unzip xs'
                    in
                        ([y|ys'], [z|zs'])
                    }
    }

    val unzip' = foldr ([], []) { (y, z) (ys, zs) -> ([y|ys], [z|zs]) }


    # partition : ('a -> BOOL) -> ['a] -> (['a], ['a])
    fun rec partition = f xs -> case xs of {
    | []      -> ([], [])
    | [x|xs'] -> let {
                     val (ys, zs) = partition f xs'
                 in
                     if f x then ([x|ys], zs) else (ys, [x|zs])
                 }
    }

    fun partition' = f -> foldr ([], []) { x (ys, zs) ->
        if f x then ([x|ys], zs) else (ys, [x|zs])
    }


    # sort : ('a -> 'a -> BOOL) -> ['a] -> ['a]
    fun rec sort = lt? xs -> case xs of {
    | []          -> []
    | [pivot|xs'] -> let {
                         val (littles, bigs) =
                                    partition { x -> lt? x pivot } xs'
                     in
                         sort lt? littles ++ [pivot] ++ sort lt? bigs
                     }
    }
}

;;



# See: PythonでもRubyみたいに配列をメソッドチェーンでつなげたい
#       https://edvakf.hatenadiary.org/entry/20090405/1238885788

val sort    = List::sort (<)
val reverse = List::reverse
val map     = List::map
val join-by = List::join-string

;;

#### 1. Pipeline Style (Like a F#, Ocaml, Elixir)

[1, 4, 3, 2] ;;
# -> [1, 4, 3, 2]

[1, 4, 3, 2] |> sort ;;
# -> [1, 2, 3, 4]

[1, 4, 3, 2] |> sort |> reverse ;;
# -> [4, 3, 2, 1]

[1, 4, 3, 2] |> sort |> reverse |> map to-s ;;
# -> ["4", "3", "2", "1"]

[1, 4, 3, 2] |> sort |> reverse |> map to-s |> join-by "-" ;;
# -> "4-3-2-1"


#### 1'. Another Pipeline Style (Like a Haskell's $-operator)
join-by "-" <| map to-s <| reverse <| sort <| [1, 4, 3, 2] ;;
# -> "4-3-2-1"


#### 2. Composition Style
(sort >> reverse >> map to-s >> join-by "-") [1, 4, 3, 2] ;;
# or
[1, 4, 3, 2] |> sort >> reverse >> map to-s >> join-by "-" ;;
# -> "4-3-2-1"


#### 2'. Another Composition Style (Like a Haskell's Point-free style)
(join-by "-" << map to-s << reverse << sort) [1, 4, 3, 2] ;;
# or
join-by "-" << map to-s << reverse << sort <| [1, 4, 3, 2] ;;
# -> "4-3-2-1"


#### 3. Classical Style (Like a LISP, Python, ... etc)
join-by "-" (map to-s (reverse (sort [1, 4, 3, 2]))) ;;
# -> "4-3-2-1"
